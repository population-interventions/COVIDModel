;;
;; Setup and functions particular to setup that don't have anywhere else to live.
;;

to setdeathrisk
  ;; risk of death associated with ageranges if they contract COVID-19
  (ifelse agerange <= 5
  [
    set riskofDeath 0
  ]
  agerange <= 15
  [
    set riskofDeath .002
  ]
  agerange <= 25
  [
    set riskofDeath .002
  ]
  agerange <= 35
  [
    set riskofDeath .002
  ]
  agerange <= 45
  [
    set riskofDeath .004
  ]
  agerange <= 55
  [
    set riskofDeath .01
  ]
  agerange <= 65
  [
    set riskofDeath .05
  ]
  agerange <= 75
  [
    set riskofDeath .05
  ]
  agerange <= 85
  [
    set riskofDeath .151
  ]
  ;; updated from department of health Report 22 - does not map directly to age deciles here
  agerange <= 95
  [
    set riskofDeath .151
  ]
  [
    set riskofDeath .151
  ])
end


to setEssentialWorkerPriority
  ifelse isWorker 
  [
    ifelse essentialJob
    [
      set EssentialWorkerPriority round (random-float essentialWorkerRange)
    ]
    [
      set EssentialWorkerPriority round (essentialWorkerRange + random-float otherWorkerRange)
    ]
  ]
  [
    set EssentialWorkerPriority 101 ;; People who don't work are never essential workers.
  ]
end


to resetlanding
  ;; ensures that resources don't start on top of one another in the model
  if any? other resources-here [
    set ycor one-of [ -30 -10 10 30 ]
    resetlanding
  ]
end

to calculatedailyrisk
  ;; estimates risk of death per day for the duration of the period of illness
  ;; used for stats more than calibrated to real world given most people die late in the illness period
  set dailyrisk ( riskofDeath / Illness_period )
end

to simul_resetPersonalVirulence
  ;; ensures that personalVirulence is within bounds
  if personalVirulence > 100 [
    set personalVirulence random-normal global_Transmissability 2.5
  ]
  if personalVirulence < 0 [
    set personalVirulence random-normal global_Transmissability 2.5
  ]
end


to loadPopDivisionTable
  let populationFileData remove-item 0 csv:from-file "population.csv"
  set popDivisionTable table:make
  let i 0
  foreach populationFileData [ x -> 
    table:put popDivisionTable i x
    set i i + 1
  ]
end


to processPopulationWorkerData
  let tableData table:values popDivisionTable
  
  set totalWorkers 0
  set totalEssentialWorkers 0
  foreach tableData [ data -> 
    set totalWorkers totalWorkers + item 5 data
    if item 4 data = 1 [
      set totalEssentialWorkers totalEssentialWorkers + item 5 data
    ]
  ]
  
  set essentialWorkerRange 100 * totalEssentialWorkers / totalWorkers
  set otherWorkerRange 100 * (totalWorkers - totalEssentialWorkers) / totalWorkers
end

to createSimulants
  let pop_index 0
  let agentsOfThisIndex 0
  
  ;; set up people in the environment and allocates characteristics to them
  ask n-of Population patches with [ pcolor = black ] [
    
    ; Iterate over cohort data to get each set of simul parameters.
    let cohortData table:get popDivisionTable pop_index
    set agentsOfThisIndex agentsOfThisIndex + 1
    
    sprout-simuls 1 [
      set size 2
      set shape "dot"
      set color cyan
      set householdUnit random 1000
      set timenow 0
      set IncubationPd int ownIncubationPeriod
      set inQuarantine 0
      set vaccineOffered 0
      set sensitivity random-float 1
      set R 0
      set vacWaitDays 0
      set isolating 0

      set agerange item 1 cohortData
      set atsi item 2 cohortData
      set comorbidityState item 3 cohortData
      set essentialJob item 4 cohortData = 1
      set isWorker item 5 cohortData <= agentsOfThisIndex
      set VaccPhase item 6 cohortData
      set VaccSubPhase item 7 cohortData

      move-to one-of patches with [ pcolor = black ]

      set personalVirulence random-normal Global_Transmissability 0.025
      if personalVirulence > 1 [
        set personalVirulence 1
      ]
      if personalVirulence < 0.01 [
        set personalVirulence 0.01
      ]
      
      set sm_vac_uptake random 100

      set asymptomatic_draw random-float 1
      set asymptomaticFlag 0
      if asymptomatic_draw < Asymptom_Prop [
        set asymptomaticFlag 1
      ]

      set requireICU 0
      set requireICU_draw random-float 1

      set wearsMask random 100 ;; resethealth resetincome calculateincomeperday calculateexpenditureperday resettrust
      set returntoschool random 100

      ;;set ownComplianceWithIsolation ( exp random-normal Mcomp SComp ) ;; log transform of compliance with isolation

      simul_resetSomeRandomParams

      setEssentialWorkerPriority
      setdeathrisk
      
      simul_resetPersonalVirulence
    ]
    
    if agentsOfThisIndex >= item 0 cohortData [ ; item 0 cohortData = cohort size
      set pop_index pop_index + 1
      set agentsOfThisIndex 0
    ]
  ]
end

to setup_households
  ;; setting households up
  ;; allocates adults to a household unit range
  ask simuls with [ agerange > 18 and agerange <= 60 ] [
    if 95 > random 100 [
      set householdUnit random 600
    ]
  ]

  ;; allocated older adults to household Units that don't include young children or teenagers
  ask simuls with [ agerange > 60 and agerange <= 80 ] [
    if 95 > random 100 [
      set householdUnit random 200 + 600
    ]
  ]

  ;; allocated older adults 80+ to household Units that don't include young children or teenagers
  ask simuls with [ agerange > 80 ] [
    if 95 > random 100 [
      set householdUnit random 300 + 600
    ]
  ]

  ;; allocates up to two adults per household
  let houseNumber 0
  let allocCount 0
  ask simuls with [ agerange > 18 and agerange <= 60 ] [
    if 95 > random 100 [
      set householdUnit houseNumber
      set allocCount allocCount + 1
      if allocCount >= 2 [
        set houseNumber houseNumber + 1
        set allocCount 0
      ]
    ]
  ]
  
  ;; Identifies students
  ask simuls with [ agerange <= 18 ] [
    if 95 > random 100 [
      set householdUnit random houseNumber
    ]
  ]

  ask simuls [
    if agerange < 20 [
      set studentFlag 1
    ]
  ]
  ;; allocates children and teenagers to a household where there are adults at least 20 years older than them and there are not more than 2 adults in the house

  ask simuls [
    simul_resetlanding
  ]
  
  ;; Enter values in household isolation table to save O(N) existence checks per tick
  ask simuls [
    table:put houseTrackedCaseTimeTable householdunit -1
  ]
end

to setGlobals
  set anxietyFactor              0
  set NumberInfected             0
  set InfectionChange            0
  set TodayInfections            0
  set YesterdayInfections        0
  set five                       0
  set fifteen                    0
  set twentyfive                 0
  set thirtyfive                 0
  set fortyfive                  0
  set fiftyfive                  0
  set sixtyfive                  0
  set seventyfive                0
  set eightyfive                 0
  set ninetyfive                 0
  set InitialReserves            0
  set AverageContacts            0
  set AverageFinancialContacts   0
  set ScalePhase                 0
  set Days                       0
  set GlobalR                    0
  set CaseFatalityRate           0
  set DeathCount                 0
  set DailyCases                 0
  set Scaled_Population          0
  set ICUBedsRequired            0
  set scaled_Bed_Capacity        0
  set currentInfections          0
  set eliminationDate            0
  set PotentialContacts          0
  set bluecount                  0
  set yellowcount                0
  set redcount                   0
  set todayInfected              0
  set cumulativeInfected         0
  set scaledPopulation           0
  set MeanR                      0
  set EWInfections               0
  set StudentInfections          0
  set meanDaysInfected           0
  set lasttransday               0
  set lastPeriod                 0
  set casesinperiod28            0
  set casesinperiod14            0
  set casesinperiod7             0
  set resetDate                  0
  set cashposition               0
  set Objfunction                0
  set decisionDate               0
  set prior0                     0
  set prior1                     0
  set prior2                     0
  set prior3                     0
  set prior4                     0
  set prior5                     0
  set prior6                     0
  set prior7                     0
  set prior8                     0
  set prior9                     0
  set prior10                    0
  set prior11                    0
  set prior12                    0
  set prior13                    0
  set prior14                    0
  set prior15                    0
  set prior16                    0
  set prior17                    0
  set prior18                    0
  set prior19                    0
  set prior20                    0
  set prior21                    0
  set prior22                    0
  set prior23                    0
  set prior24                    0
  set prior25                    0
  set prior26                    0
  set prior27                    0
  set prior28                    0

  set prior6_real 0
  set prior5_real 0
  set prior4_real 0
  set prior3_real 0
  set prior2_real 0
  set prior1_real 0
  set prior0_real 0
  set casesinperiod7_real 0
  
  set new_case_real 0
  set new_case_real_counter 0
  
  ;; These used to be dynamic controls with conflicting variable names.
  set spatial_distance 0
  set case_isolation 0
  set quarantine 0
  set contact_radius 0
  set Track_and_Trace_Efficiency 0
  set stage 0
  set prev_stage -1

  set stageHasChanged 0
  set stageToday 0
  set stageYesterday 0
  set PrimaryUpper 0
  set SecondaryLower 0
  
  set houseTrackedCaseTimeTable table:make
  set houseLocationTable table:make

  set meanIDTime 0
  set popDivisionTable 0 ; Table of population cohort data

  ; Number of agents that are workers and essential workers respectively.
  set totalWorkers 0
  set totalEssentialWorkers 0
  set essentialWorkerRange 0
  set otherWorkerRange 0

  set transmission_count 0
  set transmission_count_metric 0
  set transmission_sum 0
  set transmission_average 0
  
  ; Vaccine phase and subphase, as well as internal index and data table.
  set global_vaccinePhase 0
  set global_vaccineSubPhase 0
  set global_vaccineAvailible 0
  set global_vaccineType 0
  set global_vaccinePerDay 0
  set vaccinePhaseEndDay 0
  set vaccinePhaseIndex 0
  set vaccineTable 0
  set global_vaccine_eff 0 ;; Effectiveness of the vaccine along the three dimensions (infection rate, transmition rate, duration)

  set global_schoolActive 1 ;; Whether students ignore avoiding each other to go to school

  ;; log transform illness period variables
  set Illness_PeriodVariance 0
  set M 0
  set BetaillnessPd 0
  set S 0

  ;; log transform incubation period variables
  set Incubation_PeriodVariance 0
  set MInc 0
  set BetaIncubationPd 0
  set SInc 0

  ;; log transform compliance period variables
  set Compliance_PeriodVariance 0
  set MComp 0
  set BetaCompliance 0
  set SComp 0

  ;; file reading and draw handling
  set drawNumber 0
  set drawRandomSeed 0
  set drawList 0
end

;;*******************************************************************************************************************************
;;** Buttons **
;;*******************************************************************************************************************************

to setup
  clear-all
  
  setGlobals
  random-seed RAND_SEED ;; Read from a behaviourSpace variable.

  profiler:stop
  if profile_on [
    profiler:start
  ]
  resetMetricAverages

  rngs:init

  ;;import-drawing "Background1.png" ;; imports MSD image

  set days 0 ; used to count days

  loadPopDivisionTable
  processPopulationWorkerData
  setup_vaccineData
  
  set resetdate 7
  
  ;; illness period estimation using ln transform
  set Illness_Periodvariance se_Illnesspd
  set BetaIllnessPd ln (1 + (illness_PeriodVariance / illness_period ^ 2))
  set M (ln illness_period) - (BetaillnessPd / 2)
  set S sqrt BetaIllnessPd

  ;; illness period estimation using ln transform
  set Incubation_Periodvariance se_Incubation
  set BetaIncubationPd ln (1 + (incubation_PeriodVariance / incubation_period ^ 2))
  set MInc (ln incubation_period) - (BetaincubationPd / 2)
  set SInc sqrt BetaIncubationPd

  ask red-links [
    set color red
  ]
  ask patches [
    set pcolor black
    set houseIndex -1
    set lastInfectionUpdate -1
    set lastUtilTime -1
  ]
  ;; sets a proportion of interactions outside vs inside
  ask n-of (count patches * Outside) patches [
    set pcolor green
  ]
  ;; a beta function for testing locating many people in one place at a single time
  ask n-of Gather_Location_Count patches with [ pcolor = black ] [
    set pcolor grey
    set destination 1
  ]
  set destination_patches patches with [ destination = 1 ]

  ;; setting up the hospital
  ask n-of 1 patches [
    sprout-medresources 1
  ]
  ask medresources [
    set color white
    set shape "Health care"
    set size 1.5
    set xcor 20
    set ycor -20
  ]
  calculateScaledBedCapacity
  ask medresources [
    ask n-of Scaled_Bed_Capacity patches in-radius 5 [
      set pcolor white
    ]
  ]
  ask n-of Available_Resources patches [
    sprout-resources 1
  ]

  ;; sets up resources that people want to purchase
  ask resources [
    set color white
    set shape "square"
    set size 5
    set volume one-of [2.5 5 7.5 10 ]
    resize
    set xcor -20
    set ycor one-of [-30 -10 10 30 ]
    resetlanding
  ]

  createSimulants

  ;; set up initial infected people
  set scalephase InitialScale
  ;; sets up the initial date for looking at policy-changes

  ask n-of ( initial_cases ) simuls [
    set color red
    set timenow random int ownIncubationPeriod
    simul_updatepersonalvirulence
  ]

  ;; spend CalculateIncomePerday
  ask simuls [
    set health (100 - Agerange + random-normal 0 2)
    calculateDailyrisk
  ]

  set contact_radius 0 ;; sets contact radius of people
  set Quarantine false
  set eliminationDate 0 ; used to identify the date of elimination where no current, unrecovered cases exist
  set spatial_distance false
  set case_isolation false

  setup_households
  
  ;; this ensures that half the people in households with existing infections have also have an infection and prevents a big spike early-on
  let init_secondry_infect secondary_cases
  ask simuls [
    if init_secondry_infect > 0 and any? other simuls in-radius 2 with [ color = red ] [
      set color red
      set init_secondry_infect init_secondry_infect - 1
    ]
  ]

  ;;set tracking false ;; ensures this is set to false each time the model starts
  ;;set link_switch false ;; ensures this is set to false each timme the model starts
  ;;set schoolspolicy false ;; ensures that the schools settings don't begin before the policy trigger starts
  ;;set maskPolicy false ;; that the mask policy doesn't begin before the policy trigger starts
  ;;set assignAppEss false ;; that the assigning the App to EssentialWorkers doesn't begin before the policy trigger starts
  reset-ticks
  setupstages ;; setting up for the MJA runs

end